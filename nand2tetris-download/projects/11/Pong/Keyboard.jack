// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack
/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int addr, maxString;

    /** Initializes the keyboard. */
    function void init() {
        let addr = 24576;
        let maxString = 1386;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(addr);
    }

    /**	Waits until a key is pressed on the keyboard and released,
     *  then echoes the key to the screen, and returns the character 
     *  of the pressed key. */
    function char readChar() {
        var int c;
        while (c = 0) {
            let c = keyPressed();
        }
        while (~(keyPressed() = 0)) {}
        do Output.printChar(c);
        return c;
    }

    /**	Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its value. Also handles user backspaces. */
    function String readLine(String message) {
        var String strRet, str;
        var int c, i, len;
        let str = String.new(maxString);
        do Output.printString(message);
        while (~(c = 128)) {
            let c = 0;
            while (c = 0) {
                let c = keyPressed();
            }
            do Output.printChar(c);
            if (c = 129) {
                do str.eraseLastChar();
            } else {
                do str.appendChar(c);
            }
            while (~(keyPressed() = 0)) {}
        }
        let strRet = String.new(str.length());
        let i = 0;
        let len = str.length() - 1; // discarting enter
        while (i < len) {
            do strRet.appendChar(str.charAt(i));
            let i = i + 1;
        }
        do str.dispose();
        return strRet;
    }   

    /** Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its integer value (until the first non-digit character in the
     *  entered text is detected). Also handles user backspaces. */
    function int readInt(String message) {
        var String str;
        let str = readLine(message);
        return str.intValue();
    }
}
